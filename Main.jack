class Main{
    function void main(){
        var Background background;
        var int key, width, height;
        var int playerSize;
        var boolean exit;
        var Player player;
        var Infoboard infoboard;
        /* The size of the infoboard */
        let width = 206;
        let height = 248;
        let infoboard = Infoboard.new(304, 2, height, width);
        /* The size of the game board */
        let width = 300;
        let height = 248;
        let background = Background.new(2, 2, height, width);
        
        let playerSize = 18;
        let player = Player.new(2 + (width / 2), 3, playerSize, 15, 2 + ((height + background.getLineHeight()) / 2) - 1);

        let key = Keyboard.keyPressed();
        let exit = false;
        
        /* Game starts, output the start screen */
        do Screen.clearScreen();
        do Output.moveCursor(10, 20);
        do Output.printString("Press space to start");

        while(~(key = 0)) { // Wait until no key is pressed
            let key = Keyboard.keyPressed();
        }
        while(~(key = 32)) { // Wait until space is pressed
            let key = Keyboard.keyPressed();
        }
        while(key = 32) { // Wait until spce is released
            let key = Keyboard.keyPressed();
        }

        do Screen.clearScreen();
        do background.draw();
        do infoboard.draw();
        /* Main Program */
        while(~exit) {
            // player.draw(false);
            let key = Keyboard.keyPressed();
            if(key = 132) { // right arrow
                do player.draw(false);
                do player.moveRight(2 + width);
            }
            if(key = 130) {
                do player.draw(false);
                do player.moveLeft(2);
            }
            do player.draw(true);
            let exit = false;            
            
            do Sys.wait(100);
        }

        /* Game over, output the END screen */
        do Screen.clearScreen();
        do Output.moveCursor(10, 30);
        do Output.printString("END");

        return;
    }
}
