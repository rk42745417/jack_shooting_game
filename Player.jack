class Player{
    field int radius;
    field int speed; // pixels per move
    field Coordinate coordinate;
    
    constructor Player new(int Aposition, int Aradius, int Aspeed, int Aheight) {
        let coordinate = Coordinate.new(Aposition, Aheight);
        let radius = Aradius;
        let speed = Aspeed;
        return this;
    }
    method Coordinate getCoordinate() { return coordinate; }
    /* leftBorder is the border of the game board */
    method void moveLeft(int leftBorder) {
        if(coordinate.getX() - speed - radius < leftBorder) { // 3 is for the error
            return;
        }
        do coordinate.setX(coordinate.getX() - speed);
        return;
    }
    /* rightBorder is the border of the game board */
    method void moveRight(int rightBorder) {
        if(coordinate.getX() + speed + radius > rightBorder) {
            return;
        }
        do coordinate.setX(coordinate.getX() + speed);
        return;
    }

    method void draw(boolean isBlack) {
        do Screen.setColor(isBlack);
        do Screen.drawCircle(coordinate.getX(), coordinate.getY(), radius);
        return;
    }
    method void dispose() {
        do coordinate.dispose();
        do Memory.deAlloc(this);
        return;
    }
}
