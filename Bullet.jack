class Bullet {
    field Coordinate center;
    field int radius, speed;
    constructor Bullet new(int Ax,int Ay,int Aradius,int Aspeed){
        let center = Coordinate.new(Ax, Ay);
        let radius = Aradius;
        let speed = Aspeed;
        do draw(true);
        return this;
    }
    method void dispose() {
        do center.dispose();
        do Memory.deAlloc(this);
        return;
    }
    /* Return a new Coorinate Object of the position the bullet moved one tick at. */
    method Coordinate newCoordinate(int upperBound) {
        if(center.getY() - speed > upperBound) {
            return Coordinate.new(center.getX(), center.getY() - speed);
        }
        return null;
    }
    method void draw(boolean isBlack){
        do Screen.setColor(isBlack);
        do Screen.drawCircle(center.getX(), center.getY(), radius);
        return;
    }
    method int getX(){
        return center.getX();
    }
    method int getY(){
        return center.getY();
    }
    method int getRadius(){
        return radius;
    }
    method int getSpeed(){
        return speed;
    }
}
